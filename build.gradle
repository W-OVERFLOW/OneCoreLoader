//file:noinspection GradlePackageUpdate
plugins {
    id "java"
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'maven-publish'
}

version = "1.4.0"
group = "cc.woverflow"
archivesBaseName = rootProject.name.toLowerCase(Locale.ENGLISH)

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url "https://repo.woverflow.cc/" }
}

configurations {
    // Creates an extra configuration that implements `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom(include)
}

dependencies {
    compileOnly ("com.google.code.gson:gson:2.2.3")
    compileOnly ("net.minecraft:launchwrapper:1.12")
    include ("io.sentry:sentry:5.6.3")
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "cc.woverflow"
            artifactId = "onecore-loader"
            version = project.version

            from(components["java"])
        }
    }

    repositories {
        maven {
            name = "W-OVERFLOW"
            url = "https://repo.woverflow.cc/releases"
            credentials {
                username = getProperty("woverflow.username")
                password = getProperty("woverflow.password")
            }
        }
    }
}
